apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    def apiKeys = loadApiKeys()

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.cmgcode.minimoods"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "SENTRY_DSN", apiKeys['SENTRY_DSN']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    def lifecycle_version = "2.2.0"
    def arch_version = "2.1.0"
    def room_version = "2.2.6"
    def espresso_version = "3.3.0"
    def junit_version = "1.1.2"

    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Core
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'

    // Jetpack
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.preference:preference-ktx:1.1.1"

    // UI
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.applandeo:material-calendar-view:1.9.0-rc03'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'nl.dionsegijn:konfetti:1.2.1'

    // Support
    implementation 'io.sentry:sentry-android:3.1.0'

    // Testing
    testImplementation "androidx.test.ext:junit:$junit_version"
    testImplementation "androidx.arch.core:core-testing:$arch_version"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    testImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    testImplementation 'junit:junit:4.13.1'
    testImplementation "com.google.truth:truth:1.1"
    testImplementation "io.mockk:mockk:1.10.4"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'
    testImplementation 'org.robolectric:robolectric:4.4'

    androidTestImplementation "androidx.test.ext:junit:$junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
}

static def loadApiKeys() {
    Properties properties = new Properties()
    properties.load(new FileInputStream(new File("secrets.properties")))
    return properties
}
